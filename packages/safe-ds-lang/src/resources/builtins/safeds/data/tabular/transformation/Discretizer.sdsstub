package safeds.data.tabular.transformation

from safeds.data.tabular.containers import Table
from safeds.data.tabular.transformation import TableTransformer

/**
 * The Discretizer bins continuous data into intervals.
 *
 * @param binCount The number of bins to be created.
 *
 * @example
 * pipeline example {
 *     val table = Table({"a": [1, 2, 3, 4]});
 *     val discretizer = Discretizer(2).fit(table, ["a"]);
 *     val transformedTable = discretizer.transform(table);
 *     // Table({"a": [0, 0, 1, 1]})
 * }
 */
class Discretizer(
    @PythonName("number_of_bins") const binCount: Int = 5
) sub TableTransformer where {
    binCount >= 2
} {
    /**
     * The number of bins to be created.
     */
    @PythonName("number_of_bins") attr binCount: Int

    /**
     * Learn a transformation for a set of columns in a table.
     *
     * This transformer is not modified.
     *
     * @param table The table used to fit the transformer.
     * @param columnNames The list of columns from the table used to fit the transformer. If `null`, all columns are used.
     *
     * @result fittedTransformer The fitted transformer.
     */
    @Pure
    fun fit(
        table: Table,
        @PythonName("column_names") columnNames: List<String>?
    ) -> fittedTransformer: Discretizer

    /**
     * Learn a transformation for a set of columns in a table and apply the learned transformation to the same table.
     *
     * **Note:** Neither this transformer nor the given table are modified.
     *
     * @param table The table used to fit the transformer. The transformer is then applied to this table.
     * @param columnNames The list of columns from the table used to fit the transformer. If `null`, all columns are used.
     *
     * @result fittedTransformer The fitted transformer.
     * @result transformedTable The transformed table.
     */
    @Pure
    @PythonName("fit_and_transform")
    fun fitAndTransform(
        table: Table,
        @PythonName("column_names") columnNames: List<String>? = null
    ) -> (fittedTransformer: Discretizer, transformedTable: Table)
}
